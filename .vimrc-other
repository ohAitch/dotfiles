set nocompatible            " ignore vi-compatibility
set history=1000            " keep n lines of history
set bs=2                    " set backspace to wrap around to previous line
set whichwrap=~,[,]         " Wrap around lines with ~, left-arrow key, and right-arrow key
set number                  " enable line numbering
set numberwidth=5           " set line numbering to take up 5 lines
set cursorline              " highlights current line

" sets highlight mode to bold
highlight CursorLine cterm=bold
" set background of listchars to brown
"highlight SpecialKey ctermbg=brown

set ruler                   " show the cursor position all the time
set nowrap                  " don't wrap long lines
set textwidth=0             " don't wrap lines on inserts
set linebreak               " break on sane delimiters (like space)
set lcs=trail:·,extends:>,tab:>-   " indicates trailing spaces by '·',  wrapped lines by '>', and tabs by '>-'
set showcmd                 " display incomplete command
set scrolloff=3             " scroll up/down by 3 instead of 1
set cmdheight=1             " set command bar height
set vb t_vb=""              " Turn visual bell off
set mouse=a                 " use the mouse in console vim
set splitright              " vertical splits split to the right (instead of left)
set clipboard+=unnamed      " yanks go onto the global clipboard as well -- this might make a mapping unnecessary

" very informative status line -- but what does it do?
set statusline=%F%m%r%h%w\ [TYPE=%Y\ %{&ff}]\ [%l/%L\ (%p%%)]

set autochdir               " always switch to the current file directory
set hidden                  " puts current buffer to background without writing to disk
set autowrite               " write on make/shell, and other commands
set autoread                " reload changed files
set backup                  " simple backup
set backupdir=~/.vim/backup " stores all backups here
set directory=~/.vim/swap   " stores all swap files here

set incsearch               " turn on incremental search
set hlsearch                " highlights matches when searching
set ignorecase              " ignore case for searching
set smartcase               " exclude explicit CAPS from ignorecase

set autoindent              " Turn on auto indent
set tabstop=4               " set tab character to n space
set expandtab               " turn tabs into whitespace
set softtabstop=4           " n spaces are treated as tabs
set shiftwidth=4            " indent width for autoindent
filetype indent on          " indent depends on filetype
filetype plugin on          " enable plugins 
syntax on                   " Syntax highlighting

if has('persistent_undo')
    set undofile            " Enable persistent undo
    set undodir=~/.vim/tmp/ " Store undofiles in a tmp dir
endif

set cpoptions=yraABceFsmq
"             |||||||||||
"             ||||||||||+-- When joining lines, leave the cursor
"             |||||||||      between joined line
"             |||||||||+-- When a new match is created (showmatch)
"             ||||||||      pause for .5
"             ||||||||+-- Set buffer options when entering the
"             |||||||      buffer
"             |||||||+-- :write command updates current file name
"             ||||||+-- Automatically add <CR> to the last line
"             |||||      when using :@r
"             |||||+-- Searching continues at the end of the match
"             ||||      at the cursor position
"             ||||+-- A backslash has no special meaning in mapping
"             |||+-- :write updates alternative file name
"             ||+-- :read updates alternative file name
"             |+-- '.' can redo search ('/') commands
"             +-- '.' can redo yank commands

" Various Syntax {{{

autocmd BufRead,BufNewFile *.txt setfiletype text
autocmd BufRead,BufNewFile *.mako setfiletype mako
autocmd FileType text set wrap
autocmd FileType cpp set makeprg=g++\ -g\ %\ -o\ %<.out

"let java_highlight_java_lang_ids=1
"let java_highlight_all=1

let python_highlight_all=1
"let g:pydiction_location = '~/.vim/plugin/pydiction/complete-dict'
set tags+=tags;$HOME

" }}}


" Function Time {{{

" Syntax toggle
function! ToggleSyntax()
    if exists("g:syntax_on")
        syntax off
    else
        syntax enable
    endif
endfunction

" Tmux integration
function! TmuxWindowMotion(dir)
    let dir = a:dir

    let old_winnr = winnr()
    execute "wincmd " . dir
    if old_winnr != winnr()
        return
    endif

    if dir == 'h'
        let dir = '-L'
    elseif dir == 'j'
        let dir = '-D'
    elseif dir == 'k'
        let dir = '-U'
    elseif dir == 'l'
        let dir = '-R'
    endif
    call system('tmux select-pane ' . dir)
endfunction

" }}}

" Mapping time {{{
let mapleader = ","

" swap keys
nnoremap ' `
nnoremap ` '

" navigation in insert-mode
inoremap <C-a> <C-o>0
inoremap <C-e> <C-o>$
"inoremap <C-n> <C-o><C-n>
"inoremap <C-p> <C-o><C-p>
"inoremap <C-b> <C-o>h
"inoremap <C-f> <C-o>l

" break out of insert-mode
inoremap jj <Esc>

" When using C-u/C-w in insert mode, create a new change instead of appending 
inoremap <C-u> <C-g>u<C-u>
inoremap <C-w> <C-g>u<C-w>

" NERDTreeToggle() for current window
nnoremap <Leader>t :NERDTreeToggle<CR>

" Quickly save a file as root
cabbrev w!! w !sudo tee % > /dev/null<CR>:e!<CR><CR>

" reload .vimrc
nnoremap <Leader>r :source $MYVIMRC<CR>

" Tmux integration with window movement
" enable later
nnoremap <silent> <C-w>h :call TmuxWindowMotion('h')<cr>
nnoremap <silent> <C-w>j :call TmuxWindowMotion('j')<cr>
nnoremap <silent> <C-w>k :call TmuxWindowMotion('k')<cr>
nnoremap <silent> <C-w>l :call TmuxWindowMotion('l')<cr>

" remove highlighting
nnoremap <C-l> :set hlsearch!<CR>
" insert newline without going into insert-mode
nnoremap <CR> o<Esc>
" copy and paste using leader key
nnoremap <Leader>m "+p
nnoremap <Leader>n "+y
vnoremap <Leader>n "+y
" toggle wrap on and off
nnoremap <Leader>w :set wrap!<CR>
" toggle EOL/tab characters
nnoremap <Leader>l :set list!<CR>
" split vertically
nnoremap <Leader>v :vsp<CR>
" toggle syntax on and off -- also an ugly function
nnoremap <Leader>s :call ToggleSyntax()<CR>
set pastetoggle=,,

" }}}
